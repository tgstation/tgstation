/////////// Oldstation items

/obj/item/paper/fluff/ruins/oldstation
	name = "Cryo Awakening Alert"
	default_raw_text = "<B>**WARNING**</B><BR><BR>Catastrophic damage sustained to station. Powernet exhausted to reawaken crew.<BR><BR>Immediate Objectives<br><br>1: Activate emergency power generator<br>2: Lift station lockdown on the bridge<br><br>Please locate the 'Damage Report' on the bridge for a detailed situation report."

/obj/item/paper/fluff/ruins/oldstation/damagereport
	name = "Damage Report"
	default_raw_text = "<b>*Damage Report*</b><br><br><b>Alpha Station</b> - Destroyed<br><br><b>Beta Station</b> - Catastrophic Damage. Medical, destroyed. Atmospherics and Engine Core, partially destroyed.<br><br><b>Charlie Station</b> - Multiple asteroid impacts, no loss in air pressure.<br><br><b>Delta Station</b> - Intact. <b>WARNING</b>: Unknown force occupying Delta Station. Intent unknown. Species unknown. Numbers unknown.<br><br>Recommendation - Reestablish station powernet via solar array. Reestablish station atmospherics system to restore air."

/obj/item/paper/fluff/ruins/oldstation/protosuit
	name = "B01-MOD modular suit Report"
	default_raw_text = "<b>*Prototype MODsuit*</b><br><br>This is a prototype powered exoskeleton, a design not seen in hundreds of years, \
		the first post-void war era modular suit to ever be safely utilized by an operator. \
		This ancient clunker is still functional, though it's missing several modern-day luxuries from \
		updated Nakamura Engineering designs. Primarily, the suit's myoelectric suit layer is entirely non-existant, \
		and the servos do very little to help distribute the weight evenly across the wearer's body, \
		making it slow and bulky to move in. Additionally, the armor plating never finished production aside from the shoulders, \
		forearms, and helmet; making it useless against direct attacks. The internal heads-up display is rendered entirely in \
		monochromatic cyan, leaving the user unable to see long distances. However, the way the helmet retracts is pretty cool."

/obj/item/paper/fluff/ruins/oldstation/protohealth
	name = "Health Analyzer Report"
	default_raw_text = "<b>*Health Analyzer*</b><br><br>The portable Health Analyzer is essentially a handheld variant of a health analyzer. Years of research have concluded with this device which is \
	capable of diagnosing even the most critical, obscure or technical injuries any humanoid entity is suffering in an easy to understand format that even a non-trained health professional \
	can understand.<br><br>The health analyzer is expected to go into full production as standard issue medical kit."

/obj/item/paper/fluff/ruins/oldstation/protogun
	name = "K14 Energy Gun Report"
	default_raw_text = "<b>*K14-Multiphase Energy Gun*</b><br><br>The K14 Prototype Energy Gun is the first Energy Rifle that has been successfully been able to not only hold a larger ammo charge \
	than other gun models, but is capable of swapping between different energy projectile types on command with no incidents.<br><br>The weapon still suffers several drawbacks, its alternative, \
	non laser fire mode, can only fire one round before exhausting the energy cell, the weapon also remains prohibitively expensive, nonetheless NT Market Research fully believe this weapon \
	will form the backbone of our Energy weapon catalogue.<br><br>The K14 is expected to undergo revision to fix the ammo issues, the K15 is expected to replace the 'stun' setting with a \
	'disable' setting in an attempt to bypass the ammo issues."

/**
 * Supermatter crystal fluff paper used in Charlie station ruin
 */
/obj/item/paper/fluff/ruins/oldstation/protosupermatter
	name = "Supermatter Crystal Generator"
	default_raw_text = "<b>*Supermatter Crystal Shard*</b><br><br>Modern power generation typically comes in two forms, a Fusion Generator or a Fission Generator. Fusion provides the best space to power \
	\ntratio, and is typically seen on military or high security ships and stations, however Fission reactors require the usage of expensive, and rare, materials in its construction. \
	Fission generators are massive and bulky, and require a large reserve of uranium to power, however they are extremely cheap to operate and oft need little maintenance once \
	\ntoperational.<br><br>The Supermatter aims to alter this, a functional Supermatter is essentially a gas producer that generates far more radiation than Fusion or Fission \ntgenerators can ever hope to produce. "

/obj/item/paper/fluff/ruins/oldstation/protoinv
	name = "Laboratory Inventory"
	default_raw_text = "<b>*Inventory*</b><br><br>(1) Prototype MODsuit<br><br>(1)Health Analyser<br><br>(1)Prototype Energy Gun<br><br>(1)Singularity Generation Disk<br><br><b>DO NOT REMOVE WITHOUT \
	THE CAPTAIN AND RESEARCH DIRECTOR'S AUTHORISATION</b>"

/obj/item/paper/fluff/ruins/oldstation/report
	name = "Crew Reawakening Report"
	default_raw_text = "Artificial Program's report to surviving crewmembers.<br><br>Crew were placed into cryostasis on March 10th, 2445.<br><br>Crew were awoken from cryostasis around June, 2557.<br><br> \
	<b>SIGNIFICANT EVENTS OF NOTE</b><br>1: The primary radiation detectors were taken offline after 112 years due to power failure, secondary radiation detectors showed no residual \
	radiation on station. Deduction, primarily detector was malfunctioning and was producing a radiation signal when there was none.<br><br>2: A data burst from a nearby Nanotrasen Space \
	Station was received, this data burst contained research data that has been uploaded to our RnD labs.<br><br>3: An unknown force has occupied Delta station. Additionally, a school of common space carp have \
	taken refuge in the space surrounding all remaining stations, primarily Beta station. "

/obj/item/paper/fluff/ruins/oldstation/generator_manual
	name = "S.U.P.E.R.P.A.C.M.A.N.-type portable generator manual"
	default_raw_text = "You can barely make out a faded sentence... <br><br> Wrench down the generator on top of a wire node connected to either a SMES input terminal or the power grid."

/obj/item/paper/fluff/ruins/oldstation/protosleep
	name = "Prototype Delivery"
	default_raw_text = "<b>*Prototype Sleeper*</b><br><br>We have delivered the lastest in medical technology to the medical bay: circuitry for a new prototype sleeper. Looks like it didn't come with the parts to actually build it figures. Get engineering on this."

/obj/item/paper/fluff/ruins/oldstation/survivor_note
	name = "To those who find this"
	default_raw_text = "I was on a mission of an exploration drone reclamation, when I lost the signal. I've had just enough pressure to make it back to the station.... But this is really bad... <br><br> \
	Beta looks like a smashed tin can, and Alpha is gone completely. I didn't manage to find anyone except those sleeping beauties and something I don't even know how to explain. The blood and gore is everywhere, those things took out the entire R&D. \
	They're hissing and crawling behind the maintenance hatch that I welded off to not let them in.<br><br> \
	I had a proximity sensor with me, so I donated my left cybernetic arm to make this little fella. One of janitor's bucket served as a perfect casing for him. <br><br> \
	Here I thought that I'll die of malnutrition, when I started feeling the symptoms of hypercapnia. I will turn you off to save the battery. It's time for both us to sleep, little guy.<br><br> \
	If you're reading this, I'm probably dead. I've opened Ramboo's maintenance pannel with my ID. Please let him help to clean up my remains..."

/obj/item/paper/fluff/ruins/oldstation/biolab_note_molly
	name = "Diary note - Molly"
	default_raw_text = "It has been several months since our Molly passed away. She was our most valuable crew member, especially compared to that prick that happily threw a party to make sure `that beef won't go to waste`...<br><br> \
	Oh, how I miss her warm milk...<br><br>I've put Molly's biopsy in the fridge and almost completed the solution.<br><br> \
	Next steps:<ul><li>Pour the broth to the growing vat (beaker nearby)</li><li>Add one dropper of the solution</li><li>Add Molly's biopsy to the vat</li></ul> \
	Just need to make sure to use the correct bottle this time... I'll even mark it as `<b>Solution for Molly</b>`, or I tend to mix things up... <br>I can already feel the endorphin release from hugging her again.<br><br> \
	If everything goes well, I will try out those slimes the papers praising as the future of science. They say that the cell lines may be found on anything moldy and rotting, and these small blobs have crazy mutation potential when properly fed."

/obj/item/paper/fluff/ruins/oldstation/biolab_note_emergency
	name = "Diary note - Emergency"
	default_raw_text = "OH GOD, the station is still creaking from a heavy impact in the port direction. The power is down, coms not responding, the air supply pipe depressurized and I can feel the artificial gravity weakening. \
	The whole department is running around in panic. I'll just pray that engineers won't let the engine delaminate.<br><br> ...And the alien spawn have broken out of the containment area due to the impact and slipped into the vent.<br><br> \
	I have a bad feeling about this, but I doubt that now is the right time to make guys hunt for what they call my `pet cockroach`... And RD is scary..."

/obj/item/paper/fluff/ruins/oldstation/apc_note
	name = "DO NOT TOUCH!"
	default_raw_text = "This is a spare pre-charged APC battery for emergencies ONLY. DO NOT use it for stun prods, Bob."

/obj/machinery/mod_installer
	name = "modular outerwear device installator"
	desc = "An ancient machine that mounts a MOD unit onto the occupant."
	icon = 'icons/obj/machines/mod_installer.dmi'
	icon_state = "mod_installer"
	base_icon_state = "mod_installer"
	layer = ABOVE_WINDOW_LAYER
	use_power = IDLE_POWER_USE
	anchored = TRUE
	density = TRUE
	obj_flags = NO_BUILD // Becomes undense when the door is open
	idle_power_usage = BASE_MACHINE_IDLE_CONSUMPTION * 0.5
	active_power_usage = BASE_MACHINE_ACTIVE_CONSUMPTION * 0.3

	var/busy = FALSE
	var/busy_icon_state

	var/obj/item/mod/control/mod_unit = /obj/item/mod/control/pre_equipped/prototype

	COOLDOWN_DECLARE(message_cooldown)

/obj/machinery/mod_installer/Initialize(mapload)
	. = ..()
	occupant_typecache = typecacheof(/mob/living/carbon/human)
	if(ispath(mod_unit))
		mod_unit = new mod_unit()

/obj/machinery/mod_installer/Destroy()
	QDEL_NULL(mod_unit)
	return ..()

/obj/machinery/mod_installer/proc/set_busy(status, working_icon)
	busy = status
	busy_icon_state = working_icon
	update_appearance()

/obj/machinery/mod_installer/proc/play_install_sound()
	playsound(src, 'sound/items/rped.ogg', 30, FALSE)

/obj/machinery/mod_installer/update_icon_state()
	icon_state = busy ? busy_icon_state : "[base_icon_state][state_open ? "_open" : null]"
	return ..()

/obj/machinery/mod_installer/update_overlays()
	var/list/overlays = ..()
	if(machine_stat & (NOPOWER|BROKEN))
		return overlays
	overlays += (busy || !mod_unit) ? "red" : "green"
	return overlays

/obj/machinery/mod_installer/proc/start_process()
	if(machine_stat & (NOPOWER|BROKEN))
		return
	if(!occupant || !mod_unit || busy)
		return
	set_busy(TRUE, "[initial(icon_state)]_raising")
	addtimer(CALLBACK(src, .proc/set_busy, TRUE, "[initial(icon_state)]_active"), 2.5 SECONDS)
	addtimer(CALLBACK(src, .proc/play_install_sound), 2.5 SECONDS)
	addtimer(CALLBACK(src, .proc/set_busy, TRUE, "[initial(icon_state)]_falling"), 5 SECONDS)
	addtimer(CALLBACK(src, .proc/complete_process), 7.5 SECONDS)

/obj/machinery/mod_installer/proc/complete_process()
	set_busy(FALSE)
	var/mob/living/carbon/human/human_occupant = occupant
	if(!istype(human_occupant))
		return
	if(!human_occupant.dropItemToGround(human_occupant.back))
		return
	if(!human_occupant.equip_to_slot_if_possible(mod_unit, mod_unit.slot_flags, qdel_on_fail = FALSE, disable_warning = TRUE))
		return
	human_occupant.update_action_buttons(TRUE)
	playsound(src, 'sound/machines/ping.ogg', 30, FALSE)
	if(!human_occupant.dropItemToGround(human_occupant.wear_suit) || !human_occupant.dropItemToGround(human_occupant.head))
		finish_completion()
		return
	mod_unit.quick_activation()
	finish_completion()

/obj/machinery/mod_installer/proc/finish_completion()
	mod_unit = null
	open_machine()

/obj/machinery/mod_installer/open_machine()
	if(state_open)
		return FALSE
	..()
	return TRUE

/obj/machinery/mod_installer/close_machine(mob/living/carbon/user)
	if(!state_open)
		return FALSE
	..()
	addtimer(CALLBACK(src, .proc/start_process), 1 SECONDS)
	return TRUE

/obj/machinery/mod_installer/relaymove(mob/living/user, direction)
	var/message
	if(busy)
		message = "it won't budge!"
	else if(user.stat != CONSCIOUS)
		message = "you don't have the energy!"
	if(!isnull(message))
		if (COOLDOWN_FINISHED(src, message_cooldown))
			COOLDOWN_START(src, message_cooldown, 5 SECONDS)
			balloon_alert(user, message)
		return
	open_machine()

/obj/machinery/mod_installer/interact(mob/user)
	if(state_open)
		close_machine(null, user)
		return
	else if(busy)
		balloon_alert(user, "it's locked!")
		return
	open_machine()

/obj/effect/spawner/structure/window/reinforced/damaged
	name = "damaged reinforced window spawner"
	spawn_list = list(/obj/structure/grille, /obj/structure/window/reinforced/fulltile/damaged)

/obj/structure/window/reinforced/fulltile/damaged
	var/integrity_min_factor = 0.2
	var/integrity_max_factor = 0.8

/obj/structure/window/reinforced/fulltile/damaged/Initialize(mapload)
	. = ..()
	atom_integrity = rand(max_integrity * integrity_min_factor, max_integrity * integrity_max_factor)

/obj/item/petri_dish/oldstation
	name = "molly's biopsy"
	desc = "You can see a moldy piece of sandwich inside the dish. Maybe it helped to preserve the bacteria for that long."

/obj/item/petri_dish/oldstation/Initialize(mapload)
	. = ..()
	sample = new
	sample.GenerateSample(CELL_LINE_TABLE_COW, null, 1, 0)
	var/datum/biological_sample/contamination = new
	contamination.GenerateSample(CELL_LINE_TABLE_GRAPE, null, 1, 0)
	sample.Merge(contamination)
	sample.sample_color = COLOR_SAMPLE_BROWN
	update_appearance()

/obj/item/reagent_containers/glass/beaker/oldstation
	name = "cultivation broth"
	amount_per_transfer_from_this = 50
	list_reagents = list(
		// Required for CELL_LINE_TABLE_COW
		/datum/reagent/consumable/nutriment/protein = 10,
		/datum/reagent/consumable/nutriment = 5,
		/datum/reagent/cellulose = 5,
		// Required for CELL_LINE_TABLE_GRAPE
		/datum/reagent/toxin/slimejelly = 5,
		/datum/reagent/yuck = 5,
		/datum/reagent/consumable/vitfro = 5,
		// Supplementary for CELL_LINE_TABLE_GRAPE
		/datum/reagent/liquidgibs = 5
	)

/obj/machinery/computer/old
	name = "old computer"
	circuit = /obj/item/circuitboard/computer

/obj/machinery/computer/old/Initialize(mapload)
	icon_keyboard = pick("generic_key", "med_key")
	icon_screen = pick("generic", "comm_monitor", "comm_logs")
	. = ..()
