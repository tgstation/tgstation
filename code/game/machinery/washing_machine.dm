//dye registry, add dye colors and their resulting output here if you want the sprite to change instead of just the color.
GLOBAL_LIST_INIT(dye_registry, list(
	DYE_REGISTRY_UNDER = list(
		DYE_RED = /obj/item/clothing/under/color/red,
		DYE_ORANGE = /obj/item/clothing/under/color/orange,
		DYE_YELLOW = /obj/item/clothing/under/color/yellow,
		DYE_GREEN = /obj/item/clothing/under/color/green,
		DYE_BLUE = /obj/item/clothing/under/color/blue,
		DYE_PURPLE = /obj/item/clothing/under/color/lightpurple,
		DYE_BLACK = /obj/item/clothing/under/color/black,
		DYE_WHITE = /obj/item/clothing/under/color/white,
		DYE_RAINBOW = /obj/item/clothing/under/color/rainbow,
		DYE_MIME = /obj/item/clothing/under/rank/civilian/mime,
		DYE_CLOWN = /obj/item/clothing/under/rank/civilian/clown,
		DYE_CHAP = /obj/item/clothing/under/rank/civilian/chaplain,
		DYE_QM = /obj/item/clothing/under/rank/cargo/qm,
		DYE_LAW = /obj/item/clothing/under/suit/black,
		DYE_CAPTAIN = /obj/item/clothing/under/rank/captain,
		DYE_HOP = /obj/item/clothing/under/rank/civilian/head_of_personnel,
		DYE_HOS = /obj/item/clothing/under/rank/security/head_of_security,
		DYE_CE = /obj/item/clothing/under/rank/engineering/chief_engineer,
		DYE_RD = /obj/item/clothing/under/rank/rnd/research_director,
		DYE_CMO = /obj/item/clothing/under/rank/medical/chief_medical_officer,
		DYE_REDCOAT = /obj/item/clothing/under/costume/redcoat,
		DYE_PRISONER = /obj/item/clothing/under/rank/prisoner,
		DYE_SYNDICATE = /obj/item/clothing/under/syndicate,
		DYE_CENTCOM = /obj/item/clothing/under/rank/centcom/commander
	),
	DYE_REGISTRY_JUMPSKIRT = list(
		DYE_RED = /obj/item/clothing/under/color/jumpskirt/red,
		DYE_ORANGE = /obj/item/clothing/under/color/jumpskirt/orange,
		DYE_YELLOW = /obj/item/clothing/under/color/jumpskirt/yellow,
		DYE_GREEN = /obj/item/clothing/under/color/jumpskirt/green,
		DYE_BLUE = /obj/item/clothing/under/color/jumpskirt/blue,
		DYE_PURPLE = /obj/item/clothing/under/color/jumpskirt/lightpurple,
		DYE_BLACK = /obj/item/clothing/under/color/jumpskirt/black,
		DYE_WHITE = /obj/item/clothing/under/color/jumpskirt/white,
		DYE_RAINBOW = /obj/item/clothing/under/color/jumpskirt/rainbow,
		DYE_MIME = /obj/item/clothing/under/rank/civilian/mime/skirt,
		DYE_CHAP = /obj/item/clothing/under/rank/civilian/chaplain/skirt,
		DYE_QM = /obj/item/clothing/under/rank/cargo/qm/skirt,
		DYE_CAPTAIN = /obj/item/clothing/under/rank/captain/skirt,
		DYE_HOP = /obj/item/clothing/under/rank/civilian/head_of_personnel/skirt,
		DYE_HOS = /obj/item/clothing/under/rank/security/head_of_security/skirt,
		DYE_CE = /obj/item/clothing/under/rank/engineering/chief_engineer/skirt,
		DYE_RD = /obj/item/clothing/under/rank/rnd/research_director/skirt,
		DYE_CMO = /obj/item/clothing/under/rank/medical/chief_medical_officer/skirt,
		DYE_PRISONER = /obj/item/clothing/under/rank/prisoner/skirt,
		DYE_SYNDICATE = /obj/item/clothing/under/syndicate/skirt,
	),
	DYE_REGISTRY_GLOVES = list(
		DYE_RED = /obj/item/clothing/gloves/color/red,
		DYE_ORANGE = /obj/item/clothing/gloves/color/orange,
		DYE_YELLOW = /obj/item/clothing/gloves/color/yellow,
		DYE_GREEN = /obj/item/clothing/gloves/color/green,
		DYE_BLUE = /obj/item/clothing/gloves/color/blue,
		DYE_PURPLE = /obj/item/clothing/gloves/color/purple,
		DYE_BLACK = /obj/item/clothing/gloves/color/black,
		DYE_WHITE = /obj/item/clothing/gloves/color/white,
		DYE_RAINBOW = /obj/item/clothing/gloves/color/rainbow,
		DYE_MIME = /obj/item/clothing/gloves/color/white,
		DYE_CLOWN = /obj/item/clothing/gloves/color/rainbow,
		DYE_QM = /obj/item/clothing/gloves/color/brown,
		DYE_CAPTAIN = /obj/item/clothing/gloves/color/captain,
		DYE_HOP = /obj/item/clothing/gloves/color/grey,
		DYE_HOS = /obj/item/clothing/gloves/color/black,
		DYE_CE = /obj/item/clothing/gloves/color/chief_engineer,
		DYE_RD = /obj/item/clothing/gloves/color/grey,
		DYE_CMO = /obj/item/clothing/gloves/color/latex/nitrile,
		DYE_REDCOAT = /obj/item/clothing/gloves/color/white,
		DYE_SYNDICATE = /obj/item/clothing/gloves/combat,
		DYE_CENTCOM = /obj/item/clothing/gloves/combat
	),
	DYE_REGISTRY_SNEAKERS = list(
		DYE_RED = /obj/item/clothing/shoes/sneakers/red,
		DYE_ORANGE = /obj/item/clothing/shoes/sneakers/orange,
		DYE_YELLOW = /obj/item/clothing/shoes/sneakers/yellow,
		DYE_GREEN = /obj/item/clothing/shoes/sneakers/green,
		DYE_BLUE = /obj/item/clothing/shoes/sneakers/blue,
		DYE_PURPLE = /obj/item/clothing/shoes/sneakers/purple,
		DYE_BLACK = /obj/item/clothing/shoes/sneakers/black,
		DYE_WHITE = /obj/item/clothing/shoes/sneakers/white,
		DYE_RAINBOW = /obj/item/clothing/shoes/sneakers/rainbow,
		DYE_MIME = /obj/item/clothing/shoes/sneakers/black,
		DYE_CLOWN = /obj/item/clothing/shoes/sneakers/rainbow,
		DYE_QM = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CAPTAIN = /obj/item/clothing/shoes/sneakers/brown,
		DYE_HOP = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CE = /obj/item/clothing/shoes/sneakers/brown,
		DYE_RD = /obj/item/clothing/shoes/sneakers/brown,
		DYE_CMO = /obj/item/clothing/shoes/sneakers/brown,
		DYE_SYNDICATE = /obj/item/clothing/shoes/combat,
		DYE_CENTCOM = /obj/item/clothing/shoes/combat
	),
	DYE_REGISTRY_FANNYPACK = list(
		DYE_RED = /obj/item/storage/belt/fannypack/red,
		DYE_ORANGE = /obj/item/storage/belt/fannypack/orange,
		DYE_YELLOW = /obj/item/storage/belt/fannypack/yellow,
		DYE_GREEN = /obj/item/storage/belt/fannypack/green,
		DYE_BLUE = /obj/item/storage/belt/fannypack/blue,
		DYE_PURPLE = /obj/item/storage/belt/fannypack/purple,
		DYE_BLACK = /obj/item/storage/belt/fannypack/black,
		DYE_WHITE = /obj/item/storage/belt/fannypack/white,
		DYE_SYNDICATE = /obj/item/storage/belt/military
	),
	DYE_REGISTRY_BEDSHEET = list(
		DYE_RED = /obj/item/bedsheet/red,
		DYE_ORANGE = /obj/item/bedsheet/orange,
		DYE_YELLOW = /obj/item/bedsheet/yellow,
		DYE_GREEN = /obj/item/bedsheet/green,
		DYE_BLUE = /obj/item/bedsheet/blue,
		DYE_PURPLE = /obj/item/bedsheet/purple,
		DYE_BLACK = /obj/item/bedsheet/black,
		DYE_WHITE = /obj/item/bedsheet,
		DYE_RAINBOW = /obj/item/bedsheet/rainbow,
		DYE_MIME = /obj/item/bedsheet/mime,
		DYE_CLOWN = /obj/item/bedsheet/clown,
		DYE_CHAP = /obj/item/bedsheet/chaplain,
		DYE_QM = /obj/item/bedsheet/qm,
		DYE_LAW = /obj/item/bedsheet/black,
		DYE_CAPTAIN = /obj/item/bedsheet/captain,
		DYE_HOP = /obj/item/bedsheet/hop,
		DYE_HOS = /obj/item/bedsheet/hos,
		DYE_CE = /obj/item/bedsheet/ce,
		DYE_RD = /obj/item/bedsheet/rd,
		DYE_CMO = /obj/item/bedsheet/cmo,
		DYE_COSMIC = /obj/item/bedsheet/cosmos,
		DYE_SYNDICATE = /obj/item/bedsheet/syndie,
		DYE_CENTCOM = /obj/item/bedsheet/centcom
	),
	DYE_REGISTRY_DOUBLE_BEDSHEET = list(
		DYE_RED = /obj/item/bedsheet/red/double,
		DYE_ORANGE = /obj/item/bedsheet/orange/double,
		DYE_YELLOW = /obj/item/bedsheet/yellow/double,
		DYE_GREEN = /obj/item/bedsheet/green/double,
		DYE_BLUE = /obj/item/bedsheet/blue/double,
		DYE_PURPLE = /obj/item/bedsheet/purple/double,
		DYE_BLACK = /obj/item/bedsheet/black/double,
		DYE_WHITE = /obj/item/bedsheet/double,
		DYE_RAINBOW = /obj/item/bedsheet/rainbow/double,
		DYE_MIME = /obj/item/bedsheet/mime/double,
		DYE_CLOWN = /obj/item/bedsheet/clown/double,
		DYE_CHAP = /obj/item/bedsheet/chaplain/double,
		DYE_QM = /obj/item/bedsheet/qm/double,
		DYE_LAW = /obj/item/bedsheet/black/double,
		DYE_CAPTAIN = /obj/item/bedsheet/captain/double,
		DYE_HOP = /obj/item/bedsheet/hop/double,
		DYE_HOS = /obj/item/bedsheet/hos/double,
		DYE_CE = /obj/item/bedsheet/ce/double,
		DYE_RD = /obj/item/bedsheet/rd/double,
		DYE_CMO = /obj/item/bedsheet/cmo/double,
		DYE_COSMIC = /obj/item/bedsheet/cosmos/double,
		DYE_SYNDICATE = /obj/item/bedsheet/syndie/double,
		DYE_CENTCOM = /obj/item/bedsheet/centcom/double
	),
	DYE_LAWYER_SPECIAL = list(
		DYE_COSMIC = /obj/item/clothing/under/rank/civilian/lawyer/galaxy,
		DYE_SYNDICATE = /obj/item/clothing/under/rank/civilian/lawyer/galaxy/red
	)
))

/obj/machinery/washing_machine
	name = "washing machine"
	desc = "Gets rid of those pesky bloodstains, or your money back!"
	icon = 'icons/obj/machines/washing_machine.dmi'
	icon_state = "wm_1_0"
	density = TRUE
	state_open = TRUE
	var/busy = FALSE
	var/bloody_mess = FALSE
	var/obj/item/color_source
	var/max_wash_capacity = 5

/obj/machinery/washing_machine/examine(mob/user)
	. = ..()
	if(!busy)
		. += span_notice("<b>Right-click</b> with an empty hand to start a wash cycle.")

/obj/machinery/washing_machine/process(delta_time)
	if(!busy)
		animate(src, transform=matrix(), time=2)
		return PROCESS_KILL
	if(anchored)
		if(DT_PROB(2.5, delta_time))
			var/matrix/M = new
			M.Translate(rand(-1, 1), rand(0, 1))
			animate(src, transform=M, time=1)
			animate(transform=matrix(), time=1)
	else
		if(DT_PROB(0.5, delta_time))
			step(src, pick(GLOB.cardinals))
		var/matrix/M = new
		M.Translate(rand(-3, 3), rand(-1, 3))
		animate(src, transform=M, time=2)

/obj/machinery/washing_machine/wash(clean_types)
	. = ..()
	if(!busy && bloody_mess && (clean_types & CLEAN_TYPE_BLOOD))
		bloody_mess = FALSE
		update_appearance()
		. = TRUE

/obj/machinery/washing_machine/proc/wash_cycle()
	for(var/X in contents)
		var/atom/movable/AM = X
		AM.wash(CLEAN_WASH)
		AM.machine_wash(src)

	//if we had the ability to brainwash, remove that now
	REMOVE_TRAIT(src, TRAIT_BRAINWASHING, SKILLCHIP_TRAIT)
	busy = FALSE
	if(color_source)
		qdel(color_source)
		color_source = null
	update_appearance()

/obj/item/proc/dye_item(dye_color, dye_key_override)
	var/dye_key_selector = dye_key_override ? dye_key_override : dying_key
	if(undyeable)
		return FALSE
	if(!dye_key_selector)
		return FALSE
	if(!GLOB.dye_registry[dye_key_selector])
		log_runtime("Item just tried to be dyed with an invalid registry key: [dye_key_selector]")
		return FALSE
	var/obj/item/target_type = GLOB.dye_registry[dye_key_selector][dye_color]
	if(!target_type)
		return FALSE
	if(initial(target_type.greyscale_config) && initial(target_type.greyscale_colors))
		set_greyscale(
			colors=initial(target_type.greyscale_colors),
			new_config=initial(target_type.greyscale_config),
			new_worn_config=initial(target_type.greyscale_config_worn),
			new_inhand_left=initial(target_type.greyscale_config_inhand_left),
			new_inhand_right=initial(target_type.greyscale_config_inhand_right)
		)
	else
		icon = initial(target_type.icon)
		lefthand_file = initial(target_type.lefthand_file)
		righthand_file = initial(target_type.righthand_file)
		worn_icon = initial(target_type.worn_icon)

	icon_state = initial(target_type.icon_state)
	inhand_icon_state = initial(target_type.inhand_icon_state)
	worn_icon_state = initial(target_type.worn_icon_state)
	inhand_x_dimension = initial(target_type.inhand_x_dimension)
	inhand_y_dimension = initial(target_type.inhand_y_dimension)
	name = initial(target_type.name)
	desc = "[initial(target_type.desc)] The colors look a little dodgy."
	return target_type //successfully "appearance copy" dyed something; returns the target type as a hacky way of extending

//what happens to this object when washed inside a washing machine
/atom/movable/proc/machine_wash(obj/machinery/washing_machine/washer)
	return

/obj/item/stack/sheet/hairlesshide/machine_wash(obj/machinery/washing_machine/washer)
	new /obj/item/stack/sheet/wethide(drop_location(), amount)
	qdel(src)

/obj/item/clothing/suit/hooded/ian_costume/machine_wash(obj/machinery/washing_machine/washer)
	new /obj/item/food/meat/slab/corgi(loc)
	qdel(src)

/mob/living/simple_animal/pet/machine_wash(obj/machinery/washing_machine/washer)
	washer.bloody_mess = TRUE
	gib()

/obj/item/machine_wash(obj/machinery/washing_machine/washer)
	if(washer.color_source)
		dye_item(washer.color_source.dye_color)

/obj/item/clothing/under/dye_item(dye_color, dye_key)
	. = ..()
	if(.)
		var/obj/item/clothing/under/U = .
		can_adjust = initial(U.can_adjust)
		if(!can_adjust && adjusted) //we deadjust the uniform if it's now unadjustable
			toggle_jumpsuit_adjust()

/obj/item/clothing/under/machine_wash(obj/machinery/washing_machine/washer)
	freshly_laundered = TRUE
	addtimer(VARSET_CALLBACK(src, freshly_laundered, FALSE), 5 MINUTES, TIMER_UNIQUE | TIMER_OVERRIDE)
	..()

/obj/item/clothing/head/mob_holder/machine_wash(obj/machinery/washing_machine/washer)
	..()
	held_mob.machine_wash(washer)

/obj/item/clothing/shoes/sneakers/machine_wash(obj/machinery/washing_machine/washer)
	if(chained)
		chained = FALSE
		slowdown = SHOES_SLOWDOWN
		new /obj/item/restraints/handcuffs(loc)
	..()

/obj/machinery/washing_machine/relaymove(mob/living/user, direction)
	container_resist_act(user)

/obj/machinery/washing_machine/container_resist_act(mob/living/user)
	if(!busy)
		add_fingerprint(user)
		open_machine()

/obj/machinery/washing_machine/update_icon_state()
	if(busy)
		icon_state = "wm_running_[bloody_mess]"
		return ..()
	if(bloody_mess)
		icon_state = "wm_[state_open]_blood"
		return ..()

	var/full = contents.len ? 1 : 0
	icon_state = "wm_[state_open]_[full]"
	return ..()

/obj/machinery/washing_machine/update_overlays()
	. = ..()
	if(panel_open)
		. += "wm_panel"

/obj/machinery/washing_machine/attackby(obj/item/W, mob/living/user, params)
	if(panel_open && !busy && default_unfasten_wrench(user, W))
		return

	if(default_deconstruction_screwdriver(user, null, null, W))
		update_appearance()
		return

	else if(!user.combat_mode)
		if (!state_open)
			to_chat(user, span_warning("Open the door first!"))
			return TRUE

		if(bloody_mess)
			to_chat(user, span_warning("[src] must be cleaned up first!"))
			return TRUE

		if(contents.len >= max_wash_capacity)
			to_chat(user, span_warning("The washing machine is full!"))
			return TRUE

		if(!user.transferItemToLoc(W, src))
			to_chat(user, span_warning("\The [W] is stuck to your hand, you cannot put it in the washing machine!"))
			return TRUE
		if(W.dye_color)
			color_source = W
		update_appearance()

	else
		return ..()

/obj/machinery/washing_machine/attack_hand(mob/living/user, list/modifiers)
	. = ..()
	if(.)
		return
	if(busy)
		to_chat(user, span_warning("[src] is busy!"))
		return

	if(user.pulling && isliving(user.pulling))
		var/mob/living/L = user.pulling
		if(L.buckled || L.has_buckled_mobs())
			return
		if(state_open)
			if(istype(L, /mob/living/simple_animal/pet))
				L.forceMove(src)
				update_appearance()
		return

	if(!state_open)
		open_machine()
	else
		state_open = FALSE //close the door
		update_appearance()

/obj/machinery/washing_machine/attack_hand_secondary(mob/user, modifiers)
	. = ..()
	if(. == SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN)
		return

	if(!user.canUseTopic(src, !issilicon(user)))
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(busy)
		to_chat(user, span_warning("[src] is busy!"))
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(state_open)
		to_chat(user, span_warning("Close the door first!"))
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	if(bloody_mess)
		to_chat(user, span_warning("[src] must be cleaned up first!"))
		return SECONDARY_ATTACK_CONTINUE_CHAIN
	busy = TRUE
	if(HAS_TRAIT(user, TRAIT_BRAINWASHING))
		ADD_TRAIT(src, TRAIT_BRAINWASHING, SKILLCHIP_TRAIT)
	update_appearance()
	addtimer(CALLBACK(src, .proc/wash_cycle), 20 SECONDS)
	START_PROCESSING(SSfastprocess, src)
	return SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN

/obj/machinery/washing_machine/attack_ai_secondary(mob/user, modifiers)
	return attack_hand_secondary(user, modifiers)

/obj/machinery/washing_machine/deconstruct(disassembled = TRUE)
	if (!(flags_1 & NODECONSTRUCT_1))
		new /obj/item/stack/sheet/iron(drop_location(), 2)
	qdel(src)

/obj/machinery/washing_machine/open_machine(drop = 1)
	..()
	set_density(TRUE) //because machinery/open_machine() sets it to FALSE
	color_source = null
