#define ZLEVEL_BASE_CHANCE			10 //Not a strict chance, but a relative one
#define ZLEVEL_STATION_MODIFIER		0.5 //multiplier on the chance
#define ZLEVEL_SPACE_MODIFIER		1.5

//**************************************************************
//
// Map Datums
// --------------
// Each map can have its own datum now. This means no more
// hardcoded bullshit. Same for each Z-level.
//
// Should be mostly self-explanatory. Define /datum/map/active
// in your map file. See current maps for examples.
//
//**************************************************************

/datum/map
	var/nameShort = ""
	var/nameLong = ""
	var/list/zLevels = list()
	var/zMainStation = 1
	var/zCentcomm = 2

	//Center of thunderdome admin room
	var/tDomeX = 0
	var/tDomeY = 0
	var/tDomeZ = 0

	//nanoui stuff
	var/map_dir = ""

	//Fuck the preprocessor
	var/dorf = 0

/datum/map/New()
	. = ..()
	src.loadZLevels(src.zLevels)
	return

/datum/map/proc/loadZLevels(list/levelPaths)
	for(var/i = 1 to levelPaths.len)
		var/path = levelPaths[i]
		addZLevel(new path, i)

/datum/map/proc/addZLevel(datum/zLevel/level, z_to_use = 0)
	if(!istype(level))
		warning("ERROR: addZLevel received [level ? "a bad level of type [ispath(level) ? "[level]" : "[level.type]" ]" : "no level at all!"]")
		return
	if(z_to_use > zLevels.len)
		zLevels.len = z_to_use
	zLevels[z_to_use] = level
	if(!level.movementJammed)
		accessable_z_levels += list("[z_to_use]" = level.movementChance)

var/global/list/accessable_z_levels = list()
//This list contains the z-level numbers which can be accessed via space travel and the percentile chances to get there.
//Generated by the map datum on roundstart - and added to during the round
//This comment is a memorial to balance bickering from a long-gone TGstation - Errorage and Urist


////////////////////////////////////////////////////////////////

/datum/zLevel
	var/name = ""
	var/teleJammed = 0
	var/movementJammed = 0 //prevents you from accessing the zlevel by drifting
	var/movementChance = ZLEVEL_BASE_CHANCE

////////////////////////////////

/datum/zLevel/station
	name = "station"
	movementChance = ZLEVEL_BASE_CHANCE * ZLEVEL_STATION_MODIFIER

/datum/zLevel/centcomm
	name = "centcomm"
	teleJammed = 1
	movementJammed = 1

/datum/zLevel/space
	name = "space"
	movementChance = ZLEVEL_BASE_CHANCE * ZLEVEL_SPACE_MODIFIER

/datum/zLevel/mining
	name = "mining"

// Debug ///////////////////////////////////////////////////////

/*
/mob/verb/getCurMapData()
	src << "\nCurrent map data:"
	src << "* Short name: [map.nameShort]"
	src << "* Long name: [map.nameLong]"
	src << "* [map.zLevels.len] Z-levels: [map.zLevels]"
	for(var/datum/zLevel/level in map.zLevels)
		src << "  * [level.name], Telejammed : [level.teleJammed], Movejammed : [level.movementJammed]"
	src << "* Main station Z: [map.zMainStation]"
	src << "* Centcomm Z: [map.zCentcomm]"
	src << "* Thunderdome coords: ([map.tDomeX],[map.tDomeY],[map.tDomeZ])"
	src << "* Space movement chances: [accessable_z_levels]"
	for(var/z in accessable_z_levels)
		src << "  * [z] has chance [accessable_z_levels[z]]"
	return
*/

